plugins {
    id("java")
    id("application")
    id("jacoco")
    id("org.sonarqube") version "2.7"
    id("io.freefair.lombok") version "8.1.0"
    id("com.adarshr.test-logger") version "3.2.0"
    id("de.jansauer.printcoverage") version "2.0.0"
    id("maven-publish")
    id("org.springframework.boot") version "3.3.2"
    id("io.spring.dependency-management") version "1.1.0"
    id("com.github.spacialcircumstances.gradle-cucumber-reporting") version "0.1.25"
}

mainClassName = "com.example.featuretest.FeatureTest"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

cucumberReports {
    outputDir = file('build/html')
    buildId = '0'
    reports = files('build/reports/cucumber.json')
    testTasksFinalizedByReport = false
}


//https://github.com/cucumber/cucumber-jvm/issues/1346#issuecomment-382769136
//Pass spring / cucumber related VM arguments to JVM processes forked by gradle
tasks.run {
    if (project.hasProperty('args')) {
        def argString = project.findProperty('args') as String
        args argString.split(" +")
    }

    System.properties.each {key, val ->
        if (key instanceof String) {
            ['spring.', 'logging.', 'cucumber.'].each {
                if (key.startsWith(it)) {
                    systemProperties[key] = val
                }
            }
            if (key.startsWith('featuretest.')) {
                systemProperties[key - 'featuretest.'] = val
            }
        }
    }

    doFirst {
        println " System properties: $systemProperties"
        println " Command arguments: $args"
    }
}

dependencies {
    implementation "org.springframework:spring-web"
    implementation "org.springframework:spring-test"
    implementation "org.springframework.boot:spring-boot-test"
    implementation "org.springframework.boot:spring-boot-starter"
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.2'

    implementation "io.cucumber:cucumber-java:6.11.0"
    implementation "io.cucumber:cucumber-spring:6.11.0"

    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.commons:commons-lang3:3.15.0'

    implementation "org.hamcrest:hamcrest-all:1.3"
    implementation 'com.jayway.jsonpath:json-path:2.9.0'
    implementation 'org.awaitility:awaitility:3.1.6'
    implementation 'org.skyscreamer:jsonassert:1.5.3'
    implementation 'javax.validation:validation-api:2.0.1.Final' // Maybe upgrade to this: 'jakarta.validation:jakarta.validation-api:2.0.2'
    implementation 'org.hibernate.validator:hibernate-validator:6.0.23.Final'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.3'


    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

test {
    useJUnitPlatform()
}